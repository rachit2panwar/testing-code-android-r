apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'
apply plugin: 'kotlin-parcelize'
apply from: "$rootDir/githooks.gradle"

android {
    namespace = "biz.filmeroo.premier"

    compileSdkVersion 34
    defaultConfig {
        applicationId "biz.filmeroo.premier"
        minSdkVersion 23
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'
    }

    buildFeatures {
        viewBinding true
        compose true
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    composeOptions {
        kotlinCompilerExtensionVersion = '1.5.14'
    }

    kotlinOptions {
        jvmTarget = '17'
    }
}

dependencies {
    def retrofit_version = '2.11.0'
    def compose_bom_version = '2024.06.00'
    def lifecycle_version = '2.8.4'
    def navigation_version = '2.7.7'

    // Core
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    // Support
    implementation 'androidx.annotation:annotation:1.8.1'
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation "androidx.activity:activity-ktx:1.9.1"
    implementation "androidx.activity:activity-compose:1.9.1"
    implementation 'androidx.core:core-ktx:1.13.1'

    // Compose BOM
    implementation platform("androidx.compose:compose-bom:$compose_bom_version")
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.compose.material:material-icons-extended'
    implementation 'androidx.compose.foundation:foundation'
    implementation 'androidx.compose.runtime:runtime-livedata'

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-compose:$lifecycle_version"

    // Navigation
    implementation "androidx.navigation:navigation-compose:$navigation_version"

    // Architecture
    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$retrofit_version"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofit_version"

    // Misc
    implementation 'io.coil-kt:coil-compose:2.7.0'
    implementation 'com.google.code.gson:gson:2.11.0'

    // Dagger Hilt
    implementation("com.google.dagger:hilt-android:$hilt_version")
    kapt("com.google.dagger:hilt-android-compiler:$hilt_version")

    // Test
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.mockito:mockito-inline:5.2.0'
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.8.1'
    testImplementation 'com.nhaarman:mockito-kotlin:1.6.0'
    testImplementation 'com.squareup.assertj:assertj-android:1.2.0'
    testImplementation 'androidx.annotation:annotation:1.8.1'
    testImplementation "androidx.arch.core:core-testing:2.2.0"

    // Compose Testing
    androidTestImplementation platform("androidx.compose:compose-bom:$compose_bom_version")
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
}
